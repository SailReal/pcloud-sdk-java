/*
 * ** Notice of Modification **
 *
 * This file has been altered from its original version by the Cryptomator team.
 * For a detailed history of modifications, please refer to the version control log.
 *
 * The original file can be found at https://github.com/pCloud/pcloud-sdk-java
 *
 * --
 *
 * https://cryptomator.org/
 */

apply plugin: 'maven-publish'
apply from: rootProject.file('publishing.gradle')

group = project.getPublishProperty('groupId')
version = project.getPublishProperty('versionName')

def pomXml = {
    resolveStrategy = Closure.DELEGATE_FIRST
    name "Fork of com.pcloud.sdk:${project.getPublishProperty('artifactId')} for Cryptomator for Android"
    description "Unofficial, unaffiliated and unassociated fork of https://github.com/pCloud/pcloud-sdk-java for internal use in Cryptomator for Android."
    url 'https://github.com/cryptomator/pcloud-sdk-java'
    licenses {
        license {
            name "The Apache License v2.0"
            url 'https://github.com/pCloud/pcloud-sdk-java/LICENSE'
        }
    }
    developers {
        developer {
            id 'cryptomator'
            name 'Cryptomator'
            url 'https://cryptomator.org/'
            roles {
                role 'Maintainer of this fork'
            }
        }
        developer {
            name 'pCloud AG'
            email 'android@pcloud.com'
            organization 'pCloud AG'
            organizationUrl 'https://www.pcloud.com'
            roles {
                role 'Maintainer of the forked repository at https://github.com/pCloud/pcloud-sdk-java'
            }
        }
    }

    scm {
        connection 'https://github.com/cryptomator/pcloud-sdk-java.git'
        developerConnection 'https://github.com/cryptomator/pcloud-sdk-java.git'
        url 'https://github.com/cryptomator/pcloud-sdk-java'
    }
}

applyForPlugin("java-library") {
    java {
        withJavadocJar()
        withSourcesJar()
    }
}

applyForPlugin("com.android.library") {
    android {
        publishing {
            singleVariant("release") {
                withSourcesJar()
                withJavadocJar()
            }
        }
    }
}

afterEvaluate {
    applyForPlugin("com.android.library") {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    groupId group
                    version getPublishProperty('versionName')

                    pom.withXml {
                        asNode().children().last() + pomXml
                    }
                }
            }
        }
    }

    applyForPlugin("java-library") {
        publishing {
            publications {
                library(MavenPublication) {
                    groupId group
                    version getPublishProperty('versionName')
                    from components.java

                    pom.withXml {
                        asNode().children().last() + pomXml
                    }
                }
            }
        }
    }
}
