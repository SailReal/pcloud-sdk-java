import groovy.text.SimpleTemplateEngine
import groovy.io.FileType

apply plugin: 'java'

sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
targetCompatibility = rootProject.ext.targetCompatibilityVersion

repositories {
    mavenCentral()
}

dependencies {
    testImplementation dep.junit
    testImplementation dep.mockito
    testImplementation dep.hamcrestMatchers

    implementation dep.okhttp3
    implementation dep.gson
}

sourceSets {
    main {
        java.srcDir "${buildDir}/generated/src/main/java/"
    }
}

task generateSources {

    def inputDir = file("$projectDir/src/main/java-templates")
    File outputDir = file("$buildDir/generated/src/main/java")

    println("Processing template files in $inputDir.absolutePath")

    inputs.files(project.buildFile)
    inputs.files(fileTree(inputDir))
    inputs.files
    outputs.dir outputDir

    doFirst {
        outputDir.exists() || outputDir.mkdirs()

        def fileList = []
        inputDir.eachFileRecurse (FileType.FILES) { file ->
            fileList << file
        }

        def binding = ["project": project]
        def templateEngine = new SimpleTemplateEngine()
        for (File file : fileList){
            println("Processing template file $file.absolutePath")
            def relativeOutputPath = inputDir.toPath().relativize(file.toPath()).toString()
            println("Relative path of template file $relativeOutputPath")
            File outputFile = new File(outputDir, relativeOutputPath)
            println("Writing to $outputFile.absolutePath")
            outputFile.getParentFile().mkdirs()
            outputFile.write(templateEngine.createTemplate(file).make(binding).toString(), "UTF-8")
        }
    }
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

ext {
    bintrayPackage = 'java-core'
    artifactId = 'java-core'
}

apply from: '../bintray.gradle'
apply from: '../maven-pom.gradle'